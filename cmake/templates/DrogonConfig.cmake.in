# - Config file for the Drogon package
# It defines the following variables
#  DROGON_INCLUDE_DIRS - include directories for Drogon
#  DROGON_LIBRARIES    - libraries to link against
#  DROGON_EXECUTABLE   - the drogon_ctl executable
#  Drogon_FOUND
# This module defines the following IMPORTED target:
# Drogon::Drogon

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

find_dependency(Jsoncpp REQUIRED)
find_dependency(Trantor REQUIRED)
if(NOT ${CMAKE_SYSTEM_NAME} STREQUAL "FreeBSD" AND NOT ${CMAKE_SYSTEM_NAME} STREQUAL "OpenBSD" AND NOT WIN32)
find_dependency(UUID REQUIRED)
endif(NOT ${CMAKE_SYSTEM_NAME} STREQUAL "FreeBSD" AND NOT ${CMAKE_SYSTEM_NAME} STREQUAL "OpenBSD" AND NOT WIN32)
find_dependency(ZLIB REQUIRED)
if(@pg_FOUND@)
find_dependency(pg)
endif()
if(@SQLite3_FOUND@)
find_dependency(SQLite3)
endif()
if(@MySQL_FOUND@)
find_dependency(MySQL)
endif()
if(@Boost_FOUND@)
find_dependency(Boost)
find_package(Boost COMPONENTS filesystem system)
endif()
if(@Brotli_FOUND@)
find_dependency(Brotli)
endif()
if(@COZ-PROFILER_FOUND@)
find_dependency(coz-profiler)
endif()
if(@Hiredis_FOUND@)
find_dependency(Hiredis)
endif()
if(@BUILD_DROGON_SHARED@)
find_dependency(Threads)
endif()
if(@HAS_STD_FILESYSTEM_PATH@)
find_dependency(Filesystem)
find_package(Filesystem COMPONENTS Final REQUIRED)
endif()

# Our library dependencies (contains definitions for IMPORTED targets)

get_filename_component(DROGON_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
if(NOT TARGET Drogon::Drogon)
  include("${DROGON_CMAKE_DIR}/DrogonTargets.cmake")
  include("${DROGON_CMAKE_DIR}/DrogonUtilities.cmake")
  include("${DROGON_CMAKE_DIR}/ParseAndAddDrogonTests.cmake")
endif()

get_target_property(DROGON_INCLUDE_DIRS Drogon::Drogon INTERFACE_INCLUDE_DIRECTORIES)
set(DROGON_LIBRARIES Drogon::Drogon)
set(DROGON_EXECUTABLE drogon_ctl)
